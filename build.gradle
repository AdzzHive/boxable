apply plugin: 'java'
apply plugin: 'artifactory-publish'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = {
	->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}
version = getVersionName()
group = 'com.github.dgautier'

buildscript {
	repositories {
		maven {
			url 'http://service.office.szue.net/artifactory/plugins-release'
		}
	}
	dependencies {
		classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.2.5')
	}
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

if (project(':').hasProperty('artifactory_user')) {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				
				artifact sourceJar {
					classifier "sources"
				}
			}
		}
	}
	
	artifactory {
		contextUrl = "http://service.office.szue.net/artifactory"
		publish {
			repository {
				repoKey = 'libs-release-local'
				username = "${artifactory_user}"
				password = "${artifactory_password}"
				maven = true
			}
			defaults {
				publications ('mavenJava')
			}
		}
	}
	
	artifactoryPublish.dependsOn jar
}

repositories {
	maven {
		url 'http://service.office.szue.net/artifactory/libs-release'
	}
}

dependencies {
	compile 'org.apache.pdfbox:pdfbox:2.0.0-RC2'
	compile 'org.slf4j:slf4j-api:1.7.10'
	compile 'com.google.guava:guava:18.0'
	testCompile 'junit:junit:4.12'
}
